pkgbase=firefox-esr52-i18n-sl
pkgver=52.9.0
pkgrel=1
pkgdesc="Language pack for Firefox ru uk"
arch=(any)
license=(MPL GPL LGPL)
url="https://www.mozilla.org/firefox/"

_languages=(
  'ru     "Russian"'
  'uk     "Ukrainian"'
)

pkgname=()
source=()
_url=https://ftp.mozilla.org/pub/firefox/releases/${pkgver}esr/linux-x86_64/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=${pkgbase}-${_locale,,}

  pkgname+=($_pkgname)
  source+=("firefox-i18n-${pkgver}-${_locale}.xpi::$_url/${_locale}.xpi")
  eval "package_$_pkgname() {
    _package ${_lang}
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Firefox"
  depends=("firefox-esr52-sl>=${pkgver}")
  provides=(firefox-esr52-i18n-$1)
  conflicts=(firefox-esr52-i18n-$1)
  install -Dm644 firefox-i18n-${pkgver}-$1.xpi \
    "$pkgdir/usr/lib/firefox-esr52/browser/extensions/langpack-$1@firefox.mozilla.org.xpi"
}

sha256sums=('a26e4ef4a8e6cb7be35981b9c5a5fc642df8188bf4651cded9126dfc9e0f0538'
'0fc264377a3e704693ce6aacd6cbc758080c9c441d57b55c7953b83b9af58ee5')